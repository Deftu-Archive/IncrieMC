plugins {
    id "com.github.johnrengelman.shadow" version "$shadowVersion"
    id "com.github.gmazzo.buildconfig" version "$buildConfigVersion" apply false
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "maven-publish"
    id "java"
}

version = projectVersion
group = projectGroup
archivesBaseName = projectName

sourceCompatibility = targetCompatibility = "1.8"
compileJava.options.encoding = "UTF-8"
compileKotlin.kotlinOptions.languageVersion = "1.6"
compileKotlin.kotlinOptions.jvmTarget = "1.8"

allprojects {
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "java"

    version = projectVersion
    group = projectGroup

    configurations {
        shade
        implementation.extendsFrom(shade)
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    jar {
        enabled = false
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    shadowJar {
        archiveClassifier.set('')
        configurations = [project.configurations.shade]
        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        exclude 'LICENSE.md'
        exclude 'pack.mcmeta'
        exclude 'dummyThing'
        exclude '**/module-info.class'
        exclude '*.so'
        exclude '*.dylib'
        exclude '*.dll'
        exclude '*.jnilib'
        exclude 'ibxm/**'
        exclude 'com/jcraft/**'
        exclude 'org/lwjgl/**'
        exclude 'net/java/**'

        exclude 'META-INF/proguard/**'
        exclude 'META-INF/maven/**'
        exclude 'META-INF/versions/**'
        exclude 'META-INF/com.android.tools/**'

        exclude 'fabric.mod.json'
    }

    jar.finalizedBy(shadowJar)
}