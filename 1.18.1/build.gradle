buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
    }
    dependencies {
        classpath("net.fabricmc:fabric-loom:0.10-SNAPSHOT")
    }
}

apply plugin: "maven-publish"
apply plugin: "fabric-loom"
apply plugin: "groovy"

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    minecraft("com.mojang:minecraft:1.18.1")
    mappings("net.fabricmc:yarn:1.18.1+build.1:v2")
    modImplementation("net.fabricmc:fabric-loader:0.12.12")

    modImplementation("com.terraformersmc:modmenu:3.0.1")

    modImplementation(include("gg.essential:universalcraft-1.18-fabric:${universalCraftVersion}"))
    modImplementation(include("gg.essential:elementa-1.18-fabric:${elementaVersion}"))

    shade("com.github.KevinPriv:keventbus:${eventBusVersion}")
    shade("com.squareup.okhttp3:okhttp:${okHttpVersion}")
    shade(project(":api")) {
        exclude module: "mixin"
        exclude module: "universalcraft-1.18-fabric"
        exclude module: "elementa-1.18-fabric"
        exclude module: "log4j-api"
        exclude module: "log4j-core"
    }
}

processResources {
    inputs.property("name", projectName)
    inputs.property("id", projectId)
    inputs.property("version", projectVersion)
    inputs.property("mcversion", "1.18.1")

    filesMatching("fabric.mod.json") {
        expand(
                "name": projectName,
                "id": projectId,
                "version": projectVersion,
                "mcversion": "1.18.1"
        )
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = projectGroup
            artifactId = "$projectName-1.18.1"
            version = projectVersion

            artifact(sourcesJar)
            artifact(shadowJar) {
                afterEvaluate {
                    pom {
                        withXml {
                            def node = asNode()
                            node.dependencies."*".findAll() {
                                return it.artifactId.text() == "api" && it.groupId.text() == projectGroup
                            }.each() {
                                it.parent().remove(it)
                            }
                        }
                    }
                }
            }
        }
    }
}